PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX up: <http://purl.uniprot.org/core/>

SELECT DISTINCT ?clusterid ?familytaxonid (GROUP_CONCAT(DISTINCT ?sequence; separator=",") AS ?sequences)
WHERE
{
  VALUES ?familytaxon { taxon:2 taxon:2759 }
  VALUES ?identity { 0.5 }
  
  ?organism rdfs:subClassOf ?familytaxon .
  
  ?protein a up:Protein ;
           up:organism ?organism .
  
  ?sequenceClass a up:Sequence ;
                 rdf:value ?sequence ;
                 up:memberOf ?cluster ;
                 up:length ?length ;
                 up:sequenceFor ?protein .
  
  ?cluster up:identity ?identity .

  BIND(substr(str(?familytaxon), 34) AS ?familytaxonid)
  BIND(substr(str(?cluster), 32) AS ?clusterid)
  BIND(substr(str(?protein), 33) AS ?proteinid)
  
  FILTER NOT EXISTS {
    ?protein up:annotation ?annotation .
    ?annotation a ?annotationType .
    FILTER (?annotationType = up:Non-adjacent_Residues_Annotation || 
            ?annotationType = up:Non-terminal_Residue_Annotation)
  }
  
  FILTER ( ! contains(?sequence, 'X') && ! contains(?sequence, 'B') && ! contains(?sequence, 'Z'))
} 
GROUP BY ?clusterid ?familytaxonid